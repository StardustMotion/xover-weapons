const int XW_DOPPLER_ATTACK_AMMO = 5;//*XW_AMMOBAR_UNIT;

actor XWDopplerAttackWep : BaseXoverWep { 
	weapon.slotNumber 6
	inventory.icon "DOPPATTK" obituary "$OB_XWDOPPLERATTACK" tag "$TAG_XWDOPPLERATTACK"
	states {
		noAmmo: XWAR A 0 A_TakeInventory("CentaurFlashNoAlpha", 0xFF)
			XWAR A 0 A_TakeInventory("CentaurFlashQuarterAlpha", 0xFF)
			XWAR A 0 A_TakeInventory("CentaurFlashHalfAlpha", 0xFF)
			XWAR E 0 offset(0,32) 
			goto super::noAmmo
		select: XWAR E 0 ACS_NamedExecuteWithResult("core_weaponcolor",6767)
			goto super::select
		deselect: XWAR E 0 A_TakeInventory("varBool")
			XWAR E 0 A_SetTranslucent(1.0, 0)
			goto super::deselect
		ready: XWAR E 0
			goto super::ready
		altfire: althold: XWAR E 0 A_GiveInventory("altfireToken")
		fire: hold:
			XWAR E 0 A_JumpIf(!CallACS("XW Ammo Check", 5), "noAmmo")
			TNT1 A 0 A_TakeInventory("varBool")
			TNT1 A 0 A_PlaySoundEx("DOPPSTRT", "Weapon")
			XWAR E 9 A_GunFlash("jutsu", GFF_NOEXTCHANGE)
			TNT1 A 0 A_GiveInventory("Doppler Attack Regulator")
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Watcher",0,0,0,0,0,0,0,0,0,TID+XW_CAMERA_TID-1000)
			TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD", true) // isn't applied online but w/e
			TNT1 A 0 A_JumpIfInventory("IsBot", 1, "botKicker")
			goto marchingCube
		jutsu:
			TNT1 A 3
			TNT1 A 3 A_GiveInventory("CentaurFlashHalfAlphaGiver", 1)
			TNT1 A 3 A_GiveInventory("CentaurFlashQuarterAlphaGiver", 1)
			TNT1 A 0 A_GiveInventory("CentaurFlashNoAlphaGiver", 1)
			TNT1 A 0 ACS_NamedExecuteWithResult("core_cantseekplayer", 0, false)
			XWAR A 0 A_TakeInventory("CentaurFlashNoAlpha", 0xFF)
			XWAR A 0 A_TakeInventory("CentaurFlashQuarterAlpha", 0xFF)
			XWAR A 0 A_TakeInventory("CentaurFlashHalfAlpha", 0xFF)
			stop
		botKicker: TNT1 A 0 A_SpawnItemEx("Doppler Attack Bot")
			goto marchingCube
		
		marchingCube:
			TNT1 A 1 A_TakeInventory("DopplerAttackEnergy", 1)
			TNT1 A 0 A_JumpIfInventory("IsBot", 1, "botCheck")
			TNT1 A 0 A_CheckFlag("NOINTERACTION", "calmDown")
			TNT1 A 0 A_ScaleVelocity(0.95)
			goto checkRefire			
		calmDown:
			TNT1 A 0 A_ScaleVelocity(0.85)
			goto checkRefire
		botCheck: TNT1 A 0 A_Jump(3, "endIt")
			goto marchingCube			
		checkRefire:
			TNT1 A 0 A_JumpIfInventory("varBool", 1, "endIt")
			TNT1 A 0 A_JumpIfInventory("altfireToken", 1, "endIt")
			TNT1 A 0 A_Refire("marchingCube")
			goto endIt
		endIt:
			TNT1 A 0 A_JumpIfInventory("Doppler Attack Regulator", 1, "marchingCube")
			TNT1 A 0 A_GiveInventory("varBool")
		cameraReturn:
			TNT1 A 7 A_ChangeFlag("FORCEXYBILLBOARD", false)
			TNT1 A 0 A_GiveInventory("CentaurFlashHalfAlphaGiver", 1)
			TNT1 A 0 A_GiveInventory("CentaurFlashQuarterAlphaGiver", 1)
			XWAR A 0 ACS_NamedExecuteWithResult("core_cantseekplayer", 0, false)
			XWAR A 4
			XWAR A 4 A_TakeInventory("CentaurFlashQuarterAlpha", 0xFF)
			XWAR A 4 A_TakeInventory("CentaurFlashHalfAlpha", 0xFF)
			XWAR A 4

			XWAR BCD 2
			XWAR E 22 A_TakeInventory("altfireToken")
			goto ready}}

actor "Doppler Attack Regulator" : Powerup { powerup.duration 28 } // minimum use time
actor DopplerAttackEnergy : var8 { inventory.maxamount 140 }
actor "Doppler Attack Watcher" : watcher { renderstyle none // the camera
	states {
		spawn: TNT1 A 0 
			TNT1 A 0 ACS_NamedExecuteWithResult("XW", 34, XW_DOPPLER_ATTACK_AMMO, 140)
			TNT1 A 0 A_SetArg(0,TID-XW_CAMERA_TID) // playerNumber
			TNT1 A 0 A_SetScale(args[0]+1)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Recall",0,0,0,0,0,0,0,0,0,args[0])
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Persona",0,0,0,0,0,0,0,SXF_TRANSFERSCALE)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Kemuri")
		acsWork: TNT1 A 35 A_JumpIf(!CallACS("core_targetexists"),"end")
			loop}}
		
actor "Doppler Attack On" : item { states { pickup:
	TNT1 A 0 A_ChangeFlag("PICKUP", false) // /!\ core_nopickup /!\
	TNT1 A 0 ACS_NamedExecuteWithResult("core_unsolidplayer",0,USP_TOGGLEON)
	TNT1 A 0 ACS_NamedExecuteWithResult("core_nogravityplayer",0,true,true)
	TNT1 A 0 A_TakeInventory("varBool")
	TNT1 A 0 A_SetScale(scalex*0.5, scaley*0.5)
		stop}}
		
actor "Doppler Attack Off" : CustomInventory { states { pickup:
	TNT1 A 0 A_TakeInventory("DopplerAttackEnergy")
	TNT1 A 0 A_ChangeFlag("PICKUP", true)
	TNT1 A 0 A_SetScale(scalex/0.5, scaley/0.5)
	TNT1 A 0 A_Stop
	TNT1 A 0 ACS_NamedExecuteWithResult("core_stopplayer", 0, true)
	TNT1 A 0 A_CheckFlag("NOINTERACTION", "rematerialize")
		stop
	rematerialize: TNT1 A 0 A_GiveInventory("noInteractionOff")
		stop}}
			
actor "Doppler Attack Recall" : watcher { +DONTREFLECT +DONTBLAST +NOGRAVITY height 56 radius 16
	states { 
		spawn: TNT1 A 0 nodelay A_SetArg(0, TID+1000)
			TNT1 A 0 ACS_NamedExecuteWithResult("core_setpointer", AAPTR_TARGET, args[0])
			TNT1 A 0 Thing_ChangeTID(0,0)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerSpreadRune", 1, "spreadSplit") 
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Split")
			goto ambushWait
		spreadSplit:
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Split",64,0,0,0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Split",64,0,0,0,0,0,120)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Split",64,0,0,0,0,0,240)		
		ambushWait:
			TNT1 AAAAAAA 1 A_CheckFlag("PICKUP", "ambushAttack", AAPTR_TARGET)
			TNT1 A 0 A_JumpIf(!CallACS("core_targetexists"),"end")
			loop
		end: TNT1 A 0
			stop
		ambushAttack:
			TNT1 A 0 A_JumpIf(!CallACS("core_targetexists"),"end")
			TNT1 A 0 ACS_NamedExecuteWithResult("core_targetyawpitch",2)
			TNT1 A 0 ACS_NamedExecuteWithResult("XW", XW_WARP_2_TID, args[0], true)
			//TNT1 A 0 A_Blast(0,255,256,20)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerSpreadRune", 1, "spreadMerge") 
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Merge Kit")
			goto afterMerge
		spreadMerge:
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Merge Kit",64,0,0,0,0,0,0)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Merge Kit",64,0,0,0,0,0,120)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Merge Kit",64,0,0,0,0,0,240)
		afterMerge:
			TNT1 A 14
			TNT1 A 0 ACS_NamedExecuteWithResult("core_unsolidplayer",args[0]-999,USP_TOGGLEOFF_DAMAGE)
			TNT1 A 0 ACS_NamedExecuteWithResult("core_nogravityplayer",args[0]-999,false,true)
			TNT1 A 0 ACS_NamedExecuteWithResult("core_stopplayer", args[0]-999, false)
			stop}}

actor "Doppler Attack Damager" : watcher { 
	damagetype "DopplerAttack" obituary "$OB_XWDOPPLERATTACK" states {
		spawn: TNT1 A 0 nodelay A_Explode(8, 96, 0, 0, 32)
			TNT1 A 0 A_JumpIfInTargetInventory("PowerSpreadRune", 1, 1) stop 
			TNT1 A 0 A_Explode(8, 96, 0, 0, 32)
			stop}}
		
actor "Doppler Attack Damager 0" : item { states {
	pickup: TNT1 A 0 A_SpawnItemEx("Doppler Attack Damager",-24,-24,24)
		buzz: TNT1 A 0 A_JumpIfInventory("Doppler Attack Buzz", 1, 3)
			TNT1 A 0 A_PlaySoundEx("DOPPBUZZ", "SoundSlot5")
			TNT1 A 0 A_GiveInventory("Doppler Attack Buzz")
			TNT1 A 0
			stop}}
actor "Doppler Attack Damager 1" : "Doppler Attack Damager 0" { states {
	pickup: TNT1 A 0 A_SpawnItemEx("Doppler Attack Damager",24,24,24)
		goto buzz}}
		
actor "Doppler Attack Buzz" : Powerup { powerup.duration 21 }
		
actor "Doppler Attack Bot" : watcher { states {
		spawn: TNT1 A 1 A_GiveToTarget("Doppler Attack Bot FTW")
			TNT1 A 0 A_CheckFlag("PICKUP", "end", AAPTR_TARGET)
			TNT1 A 0 A_JumpIf(!CallACS("core_targetexists"),"end")
			loop}}
actor "Doppler Attack Bot FTW" : item { states { pickup:	
	TNT1 A 0 A_ChangeVelocity(16*cos(pitch), 0, 2+(14*-sin(pitch)), CVF_RELATIVE | CVF_REPLACE) stop }}
		
		
			// ################################ //
			//			VISUALS / CLIENT		//
			// ################################ //
			
actor "Doppler Attack Persona" : "XW CS Frame" { alpha 0.5 -NOINTERACTION
	height 56 radius 16 translation "192:192=249:249", "198:198=5:5" states {
		spawn: TNT1 A 0
				TNT1 A 0 A_SetArg(0, scalex-1)
				TNT1 A 0 A_SetMass(args[0]+1)
				TNT1 A 0 A_SetScale(2.5)
			goto clientSideCheck
		otherPOV: TNT1 A 0
			stop
		clientSidePOV:	
			TNT1 A 0 A_Stop
			XWAQ V 0 ACS_NamedExecuteWithResult("core_setpointer", AAPTR_TARGET, args[0]+1000)
			
		aolGuy:
			XWAQ V 3 A_FadeIn(0.5)
			TNT1 A 1
			TNT1 A 0 A_CheckFlag("PICKUP", "end", AAPTR_TARGET)
			XWAQ V 3 A_FadeOut(0.5)
			TNT1 A 1
			TNT1 A 0 A_CheckFlag("PICKUP", "end", AAPTR_TARGET)
			TNT1 A 0 A_JumpIf(!CallACS("core_targetexists"),"end")
			loop}}
		
actor "Doppler Attack Merge Kit" : watcher { +NOINTERACTION  
	obituary "$OB_XWDOPPLERATTACKJUTSU" damagetype "DopplerAttackJutsu" states { spawn: TNT1 A 0
	TNT1 A 0 A_PlaySoundEx("DOPPBRST", "Weapon")
	TNT1 A 0 A_SpawnItemEx("Doppler Attack Merge")
	TNT1 A 14 A_SpawnItemEx("Doppler Attack Kemuri")
	TNT1 A 0 A_Explode(72,256,0,0,64)
	TNT1 A 0 A_SpawnItemEx("Doppler Attack Burst",0,0,-64) stop}}
	
const int XW_DOPP_R = 192;
const int XW_DOPP_R_Z = 192*0.7;
const int XW_DOPP_S = XW_DOPP_R/14;
const int XW_DOPP_S_Z = XW_DOPP_S*0.7;

actor "Doppler Attack Merge" : gfx {
	states { spawn: TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("Doppler Attack In",XW_DOPP_R,0,XW_DOPP_R_Z,0,0,-XW_DOPP_S_Z,135)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack In",XW_DOPP_R,0,XW_DOPP_R_Z,0,0,-XW_DOPP_S_Z,-135)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack In",XW_DOPP_R,0,-XW_DOPP_R_Z,0,0,+XW_DOPP_S_Z,135)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack In",XW_DOPP_R,0,-XW_DOPP_R_Z,0,0,+XW_DOPP_S_Z,-135)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack In",XW_DOPP_R,0,XW_DOPP_R_Z,0,0,-XW_DOPP_S_Z,45)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack In",XW_DOPP_R,0,XW_DOPP_R_Z,0,0,-XW_DOPP_S_Z,-45)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack In",XW_DOPP_R,0,-XW_DOPP_R_Z,0,0,+XW_DOPP_S_Z,45)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack In",XW_DOPP_R,0,-XW_DOPP_R_Z,0,0,+XW_DOPP_S_Z,-45)
			stop}}
			
actor "Doppler Attack Split" : "Doppler Attack Merge" {
	states { spawn: TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Out",0,0,0,0,0,+XW_DOPP_S_Z,-45)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Out",0,0,0,0,0,+XW_DOPP_S_Z,45)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Out",0,0,0,0,0,-XW_DOPP_S_Z,-45)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Out",0,0,0,0,0,-XW_DOPP_S_Z,45)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Out",0,0,0,0,0,+XW_DOPP_S_Z,-135)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Out",0,0,0,0,0,+XW_DOPP_S_Z,135)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Out",0,0,0,0,0,-XW_DOPP_S_Z,-135)
			TNT1 A 0 A_SpawnItemEx("Doppler Attack Out",0,0,0,0,0,-XW_DOPP_S_Z,135)
			stop}}
			
actor "Doppler Attack Out" : gfx {  translation "192:192=249:249", "198:198=5:5"
	states { 
		spawnframe: TNT1 A 0 A_ChangeVelocity(XW_DOPP_S,0,velz,CVF_REPLACE|CVF_RELATIVE)
			XWAQ V 14
			stop}}
			
actor "Doppler Attack In" : "Doppler Attack Out" { states { 
	spawnframe: TNT1 A 0 A_SetAngle(angle+180) goto super::spawnframe}}
			
actor "Doppler Attack Burst" : gfx { scale 8.5 +FORCEXYBILLBOARD
	translation "201:201=249:249", "110:110=5:5" states { spawnframe: TNT1 A 0
		COPY FGHIJK 2
			stop}}
			
actor "Doppler Attack Kemuri" : gfx { scale 3.5
	translation "192:192=81:81", "198:198=93:93", "199:199=159:159" states { 
	spawn: TNT1 A 0
		XWAQ QRSTU 2
		stop}}