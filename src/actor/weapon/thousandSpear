const int XW_THOUSAND_SPEAR_STAB_SPD = 32;
const int XW_THOUSAND_SPEAR_THRUST_SPD = 48;
const int XW_THOUSAND_SPEAR_ANGLE = 8;
const int XW_THOUSAND_SPEAR_LATERAL = 80;
const int XW_THOUSAND_SPEAR_AMMO = 1.5;//*XW_AMMOBAR_UNIT;
const int XW_THOUSAND_SPEAR_AMMO_2 = 1.2;//*XW_AMMOBAR_UNIT;
const int XW_THOUSAND_SPEAR_DELAY = 32;

// BASE weapon
actor XWThousandSpearBaseWep : BaseXoverWep { 
	weapon.slotNumber 4 //3
	states {
		botDecision:
			"----" A 0 A_Jump(140, "altfire")
			"----" A 0 A_Jump(256, "firePlayer")
			goto firePlayer
			
		botDecisionFury:
			"----" A 0 A_Jump(11, "finalThrust")
			"----" A 0 A_Jump(256, "stab")
			goto stab
			
		ready:
			TNT1 A 0 A_TakeInventory("varBool", 1)
			TNT1 A 0 A_GunFlash("animation", GFF_NOEXTCHANGE)
			goto super::ready
			
		noAmmo:
			TNT1 A 0 offset(0,32) A_TakeInventory("varBool", 1)
			TNT1 A 0 A_GunFlash("animation", GFF_NOEXTCHANGE)
			goto super::noAmmo
			
		deselect:
			TNT1 A 0 A_TakeInventory("varBool", 1)
			TNT1 A 0 A_TakeInventory("var16", 10)
			TNT1 A 0 A_TakeInventory("var8", 3)
			goto super::deselect
}}
			
actor XWThousandSpearWep : XWThousandSpearBaseWep {
	inventory.icon "THSNSPR1" obituary "$OB_XWTHOUSANDSPEAR" tag "$TAG_XWTHOUSANDSPEAR"
	states {	
		select: XWAH W 0 ACS_NamedExecuteWithResult("core_weaponcolor",6744)
			goto super::select	
		animation:
			XWAE DDEEFFEE 1 A_JumpIfInventory("varBool", 1, "noflash")
			loop
			
		altfire: altHold:
			"----" A 0 A_GiveInventory("altfireToken", 1)
			goto firePlayer
		fire:
			"----" A 0 A_JumpIfInventory("isBot", 1, "botDecision")
		firePlayer:
			"----" A 0 A_JumpIf(!CallACS("XW Ammo Check", XW_THOUSAND_SPEAR_AMMO), "noAmmo")
			"----" A 0 ACS_NamedExecuteAlways("XW Ammo Use", 0, XW_THOUSAND_SPEAR_AMMO)
			"----" A 0 A_JumpIfInventory("altfireToken", 1, "altRelease")
		furyCheck: hold:
			"----" A 1 offset(0,32)
			"----" A 0 A_Refire(1) goto finalThrust
			"----" A 1 offset(2,36)
			"----" A 0 A_Refire(1) goto finalThrust
			"----" A 1 offset(5,42)
			"----" A 0 A_Refire(1) goto finalThrust
			"----" A 1 offset(9,50)
			"----" A 0 A_Refire(1) goto finalThrust
			"----" A 1 offset(15,62)
			"----" A 0 A_Refire(1) goto finalThrust
			"----" A 1 offset(23,78)
			"----" A 0 A_Refire(1) goto finalThrust
			"----" A 1 offset(34,100)
			"----" A 0 A_Refire(1) goto finalThrust
			"----" A 1 offset(48,128)
			"----" A 0 A_Refire("ammoTollNoFlash")
			goto finalThrust
			
		altRelease:
			"----" A 1 offset(0,32)
			goto finalThrust			
		finalThrust: 
			//"----" A 0 A_WeaponReady(WRF_NOFIRE | WRF_NOSWITCH)
			XWAE I 0 A_Recoil(-9)
			"----" A 0 A_GiveInventory("varBool", 1)
			"----" A 0 A_PlaySoundEx("THSNSPR2","Weapon")
			"----" A 0 A_SpawnItemEx("Thousand Spear Thrust", 42*cos(pitch), 8, 24+(42*sin(-pitch)), 
				XW_THOUSAND_SPEAR_THRUST_SPD*cos(pitch), 0, XW_THOUSAND_SPEAR_THRUST_SPD*sin(-pitch), 0, SXF_TRANSFERPITCH  | SXF_TRANSFERTRANSLATION)
			"----" A 0 A_JumpIfInventory("PowerSpreadRune", 1, 1) 
			goto afterThrust
			"----" A 0 A_SpawnItemEx("Thousand Spear Thrust", 42*cos(pitch), 8, 24+(42*sin(-pitch)), XW_THOUSAND_SPEAR_THRUST_SPD*cos(pitch), 0, XW_THOUSAND_SPEAR_THRUST_SPD*sin(-pitch), -20, SXF_TRANSFERPITCH  | SXF_TRANSFERTRANSLATION)
			"----" A 0 A_SpawnItemEx("Thousand Spear Thrust", 42*cos(pitch), 8, 24+(42*sin(-pitch)), XW_THOUSAND_SPEAR_THRUST_SPD*cos(pitch), 0, XW_THOUSAND_SPEAR_THRUST_SPD*sin(-pitch), 20, SXF_TRANSFERPITCH  | SXF_TRANSFERTRANSLATION)
		afterThrust:
			"----" A 1 Offset(0,92)
			"----" A 1 Offset(0,77)
			"----" A 1 Offset(0,62)
			"----" A 1 Offset(0,47)
			"----" A 1 Offset(0,32)	
			"----" A 12 Offset(0,32) 
		furyEnd:			
			"----" A 0 A_TakeInventory("altfireToken")
			"----" A 1 Offset(10,47)
			"----" A 1 Offset(20,62)
			"----" A 1 Offset(30,77)
			"----" A 1 Offset(40,92)
			"----" A 0 A_ClearReFire
			goto ready
			
			
		ammoTollNoFlash:
			"----" A 0 A_GiveInventory("varBool", 1)		
		ammoToll:
			"----" A 0 A_JumpIfInventory("isBot", 1, "botDecisionFury")
			"----" A 0 A_JumpIfInventory("var16", 1, "stab")
			"----" A 0 A_JumpIf(!CallACS("XW Ammo Check", XW_THOUSAND_SPEAR_AMMO), "finalThrust")
			goto giveStabs
		giveStabs:
			"----" A 0 ACS_NamedExecuteAlways("XW Ammo Use", 0, XW_THOUSAND_SPEAR_AMMO)
			"----" A 0 A_GiveInventory("var16", 2)
			goto stab+1
			
		stab:
			"----" A 0 A_TakeInventory("var16", 1)
			"----" A 0 A_Recoil(-2)
			"----" A 0 A_PlaySoundEx("THSNSPR1","Weapon")
			
			"----" A 0 A_SetArg(0,random(-1,1))			
			"----" A 0 A_GiveInventory("Thousand Spear Stab Setup")
	
			"----" A 0 A_SpawnItemEx("Thousand Spear Stab", args[1], 8, args[2], args[3], 0, args[4], 0, SXF_TRANSFERPITCH | SXF_TRANSFERTRANSLATION)			
			"----" A 0 A_SpawnItemEx("Thousand Spear Stab Predict", 0,0,0, 0,0,0, args[4]+24, SXF_TRANSFERPITCH | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEANGLE,0, 6770+args[0]+4)				
			"----" A 0 A_SpawnItemEx("Thousand Spear Stab Predict", 0,0,0, 0,0,0, args[4]+24, SXF_TRANSFERPITCH | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEANGLE,0, 6770+args[0]+7)
			"----" A 0 A_JumpIfInventory("PowerSpreadRune", 1, 1)
			goto afterStab
			"----" A 0 A_SpawnItemEx("Thousand Spear Stab", args[1], 8, args[2], args[3], 0, args[4], 20, SXF_TRANSFERPITCH | SXF_TRANSFERTRANSLATION)			
			"----" A 0 A_SpawnItemEx("Thousand Spear Stab", args[1], 8, args[2], args[3], 0, args[4], -20, SXF_TRANSFERPITCH | SXF_TRANSFERTRANSLATION)			
			"----" A 0 A_SpawnItemEx("Thousand Spear Stab Predict", 0,0,0, 0,-1,0, args[4]+24, SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPITCH | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEANGLE,0, 6770+args[0]+4)
			"----" A 0 A_SpawnItemEx("Thousand Spear Stab Predict", 0,0,0, 0,1,0, args[4]+24,  SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPITCH | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEANGLE,0, 6770+args[0]+4)				
			"----" A 0 A_SpawnItemEx("Thousand Spear Stab Predict", 0,0,0, 0,1,0, args[4]+24,  SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPITCH | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEANGLE,0, 6770+args[0]+7)				
			"----" A 0 A_SpawnItemEx("Thousand Spear Stab Predict", 0,0,0, 0,-1,0, args[4]+24,  SXF_ABSOLUTEVELOCITY | SXF_TRANSFERPITCH | SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEANGLE,0, 6770+args[0]+7)
			
		afterStab:
			"----" A 0 A_JumpIf(!args[0], "fwd")
			"----" A 0 A_JumpIf(args[0] == -1, "down")
			goto up
		
		up: 
			XWAE I 1 Offset(0,92)
			"----" A 1 Offset(0,77)
			"----" A 1 Offset(0,62)
			"----" A 1 Offset(0,47)
			"----" A 1 Offset(0,32)
			goto furyRefire
		
		fwd: 
			XWAE J 1 Offset(0,92)
			"----" A 1 Offset(0,87)
			"----" A 1 Offset(0,82)
			"----" A 1 Offset(0,77)
			"----" A 1 Offset(0,72)
			goto furyRefire
		
		down:
			XWAE J 1 Offset(0,92)
			"----" A 1 Offset(0,97)
			"----" A 1 Offset(0,102)
			"----" A 1 Offset(0,107)
			"----" A 1 Offset(0,112)
			goto furyRefire
			
		furyRefire:
			"----" A 0 A_Refire("ammoToll")
			goto finalThrust}}
			
actor "Thousand Spear Stab" : proj {
	+RIPPER radius 7 height 10 damage (7) +DONTBLAST +FORCEXYBILLBOARD
	renderstyle "translucent" alpha 0.4 species "ThousandSpear" +THRUSPECIES speed 32 //XW_THOUSAND_SPEAR_SPD
	damagetype "ThousandSpear"  obituary "$OB_XWTHOUSANDSPEAR" 
		states { spawn: TNT1 A 0
			XWAD WVUU 1 A_FadeIn(0.15)
			stop}}
					
actor "Thousand Spear Stab Predict" : watcher { var int user_spreadRune; states {
	spawn: TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_spreadRune", vely*48)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SetArg(0, TID-6770) // [0,2]*[00,01,10,11]
		TNT1 A 0 A_SetMass((args[0]/3))
		TNT1 A 0 A_SetArg(0, (args[0]%3)-1)
		TNT1 A 0 A_GiveInventory("Thousand Spear Stab Setup")
		TNT1 A 0 Thing_ChangeTID(0,0)
		TNT1 A 0 A_SetArg(0, 8+
			((1+(mass/3)) * XW_THOUSAND_SPEAR_LATERAL*(((mass%2)*2)-1))) // yPos
		
		TNT1 A 0 A_SetTics(XW_THOUSAND_SPEAR_DELAY*(1+(mass/3)))
		TNT1 A 0 A_Warp(AAPTR_TARGET,0,user_spreadRune,0,0,WARPF_NOCHECKPOSITION)
		TNT1 A 0 A_JumpIf(true, "stab")
		goto stab
	stab:
		"----" A 0 A_SpawnItemEx("Thousand Spear Stab", args[1], args[0], args[2], 
			args[3], 0, args[4], 0,  SXF_TRANSFERPITCH | SXF_TRANSFERTRANSLATION) stop}}
			
			
actor "Thousand Spear Thrust" : "Thousand Spear Stab" { alpha 0.4 damage (18) height 26 radius 18
	speed 48 // XW_THOUSAND_SPEAR_THRUST_SPD
	damagetype "ThousandSpearThrust" states {
		spawn: 
			TNT1 A 0
			XWAD XYZYY 1 A_FadeIn(0.12)
		stopIt:
			XWAD X 0 A_Stop
			XWAD X 1 A_ChangeFlag("NOINTERACTION", 1)
			XWAD XX 2 A_FadeOut(0.3)
			stop}}
			
		
actor "Thousand Spear Stab Setup" : item {
	states { pickup: 
		"----" A 0 A_SetArg(1,35*cos(pitch)) // xPos
		"----" A 0 A_SetArg(2,24+(35*sin(-pitch))) // zPos
		"----" A 0 A_SetArg(3, XW_THOUSAND_SPEAR_STAB_SPD*cos(pitch)) // xVel
		"----" A 0 A_SetArg(4,  // zVel
				XW_THOUSAND_SPEAR_STAB_SPD*sin(-pitch+(args[0]*XW_THOUSAND_SPEAR_ANGLE))) 
		stop}}
		