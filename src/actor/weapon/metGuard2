actor XWMetGuard2Wep : BaseXoverWep { 
    weapon.slotNumber 7
	inventory.icon "METGARD2" obituary "$OB_XWMETGUARD2" tag "$TAG_XWMETGUARD2"
	states {
		noAmmo:
			XWAA Y 0 offset(0,32) A_GiveInventory("StopShield", 1)
			goto super::noAmmo
		select: 
			XWAA Y 0 ACS_NamedExecuteWithResult("core_weaponcolor",6736)
			goto super::select
		ready: 
			XWAA Y 0
			goto super::ready
		deselect:
			XWAA Y 0 offset(0,32) A_TakeInventory("StopShield", 1)
			goto super::deselect
		
		altfire:
			"----" A 0 A_GiveInventory("altfireToken")
		fire:
			"----" A 0 A_TakeInventory("StopShield", 1)
			"----" A 0 A_JumpIf(!CallACS("XW Ammo Check", XW_METGUARD_AMMO), "noAmmo")	
						
			"----" A 0 A_GiveInventory("MetGuardRotatableShieldSetup", 1)			
			"----" A 0 ACS_NamedExecuteAlways("XW Ammo Use", 0, XW_METGUARD_AMMO)
			"----" A 0 A_PlaySoundEx("METSHELD", "Weapon")
			//"----" A 0 ThrustThingZ(0, 12, 1, 1)
			XWAA YY 1 Offset(0,32) //A_JumpIfInventory("varBool", 1, "stopShield")	
			"----" A 0 A_GunFlash("shieldGFX", GFF_NOEXTCHANGE)			
			XWAA Z 1 Offset(0,18) A_JumpIfInventory("varBool", 1, "stopShield")
			"----" A 1 Offset(0,4) A_JumpIfInventory("varBool", 1, "stopShield")
			XWAB A 0 Offset(0,-50) ThrustThingZ(0, 12, 1, 1)
		handsLifted:
			"----" A 0 A_Refire(1)
			"----" AAAA 1 A_JumpIfInventory("varBool", 1, "stopShield")
			"----" A 0 ThrustThingZ(0, 12, 1, 1)
			"----" A 0 A_Refire(1)
			"----" AAAA 1 A_JumpIfInventory("varBool", 1, "stopShield")
			"----" A 0 ThrustThingZ(0, 12, 1, 1)
			"----" A 0 A_Refire(1)
			"----" AAAA 1 A_JumpIfInventory("varBool", 1, "stopShield")
			"----" A 0 ThrustThingZ(0, 12, 1, 1)
			"----" A 0 A_Refire(1)
			"----" AAAA 1 A_JumpIfInventory("varBool", 1, "stopShield")
			"----" A 0 A_Refire("shield")
			goto shield
			
		shield: hold:
			"----" A 0 A_JumpIfInventory("var16", 11, "stopShield")
			"----" A 0 ThrustThingZ(0, 12, 1, 1)
			"----" A 0 A_GiveInventory("var16", 1)
			"----" AAAA 1 A_JumpIfInventory("varBool", 1, "stopShield")
			"----" A 0 A_JumpIfInventory("altfireToken", 1, "stopShield")
			"----" A 0 A_ReFire
			goto stopShield
			
		althold: stopShield:
			XWAB A 0 A_GiveInventory("StopShield", 1)
			"----" A 0 Offset(0,-50) A_JumpIfInventory("varBool", 1, "fullSynchro")
			goto endQuick
            
		fullSynchro:
			"----" A 0 A_SpawnItemEx("MetGuard2ShockWave")
			"----" A 0 A_JumpIfInventory("PowerSpreadRune", 1, 1) goto fullSynchro2
			"----" A 0 A_SpawnItemEx("MetGuard2ShockWave",0,0,0,0,0,0,20)
			"----" A 0 A_SpawnItemEx("MetGuard2ShockWave",0,0,0,0,0,0,-20)
		fullSynchro2:
			"----" A 0 A_SpawnItemEx("Met Guard 2 GFX Active", 40)
			goto end
			
		end:
			"----" A 25
		endQuick:
			"----" A 3
			XWAA Z 3 Offset(0,-3)
			"----" A 3 Offset(0,9)
			"----" A 3 Offset(0,20)
		refresh:	
			XWAA Y 14 Offset(0,32)
			"----" A 0 A_ClearReFire
			"----" A 0 A_TakeInventory("altfireToken", 1)
			"----" A 0 A_TakeInventory("var16", 0xFFFF)
			"----" A 0 A_TakeInventory("varBool", 1)
			"----" A 0 A_TakeInventory("StopShield", 1)
			goto ready
			
		shieldGFX: TNT1 A 0 A_GiveInventory("MetGuard Slow Speed", 1)
		shieldGFX2:
			TNT1 A 0 A_SpawnItemEx("Met Guard 2 GFX", 40, 0, 0, velx, vely, 0, 0, SXF_ABSOLUTEVELOCITY)
			TNT1 A 1 A_JumpIfInventory("StopShield", 1, "stopGFX")
			TNT1 A 0 A_JumpIfHealthLower(1, "stopGFX")
			loop
		stopGFX: TNT1 A 0 A_TakeInventory("MetGuard Slow Speed", 1)
			stop}}

			
actor "Met Guard 2 GFX" : "Met Guard GFX" {
	translation "199:199=43:43", "245:245=14:14", "195:195=243:243", "192:192=73:73", "198:198=41:41" }
		
actor "Met Guard 2 GFX Active" : "Met Guard GFX Active" {
	translation "199:199=43:43", "245:245=14:14", "195:195=243:243", "192:192=73:73", "198:198=41:41" }
	
actor MetGuard2ShockWave : MetGuard1ShockWave { //speed 18
	states {
		Spawn:
			TNT1 A 0 noDelay A_ChangeVelocity(12, 0, 0, CVF_REPLACE | CVF_RELATIVE)
			TNT1 A 0 A_SetUserVar("user_waveCD", 8)
			TNT1 A 0 A_Jump(256, "watchMeGo")
			goto watchMeGo				
		makeWave:
			TNT1 A 0 A_SetUserVar("user_waveCD", 8)
			TNT1 A 0 A_SpawnItemEx("MetGuard2ShockWavePart")
			TNT1 A 0 A_Jump(256, "watchMeGo")
			goto watchMeGo }}
			
			
actor MetGuard2ShockWavePart : MetGuard1ShockWavePart {
		states {
			Spawn: TNT1 A 0
					TNT1 A 0 A_SpawnItemEx("MetGuard2ShockWavePartGFX")
				   TNT1 A 0 A_SpawnItemEx("MetGuard2ShockWaveSubPart", 0)
				   goto super::splash }}
                   
	
actor MetGuard2ShockWavePartGFX : MetGuardShockWavePartGFX {	
	translation "194:194=41:41", "192:192=173:173", "198:198=43:43" }

actor MetGuard2ShockWaveSubPart : MetGuard1ShockWaveSubPart { obituary "$OB_XWMETGUARD2"
		damage (22) damagetype "MetGuard2" }
	
	
	
	