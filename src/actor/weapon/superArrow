const int XW_SUPER_ARROW_AMMO = 3.25;//*XW_AMMOBAR_UNIT;
//const int XW_SUPER_ARROW_SPEED = 1; // initial
const int XW_SUPER_ARROW_H_ANGLE = 8;
const int XW_SUPER_ARROW_V_ANGLE = 5;
const int XW_SUPER_ARROW_CAP_TOP = 360-XW_SUPER_ARROW_V_ANGLE;
const int XW_SUPER_ARROW_CAP_BOT = XW_SUPER_ARROW_V_ANGLE;
const int XW_SUPER_ARROW_INVUL_SPEED = 20;
const int XW_SUPER_ARROW_MAX_SPEED = 69;

// needs a voxel :o
actor XWSuperArrowWep : BaseXoverWep { 
	weapon.slotNumber 2
	inventory.icon "SUPRAROW" obituary "$OB_XWSUPERARROW" tag "$TAG_XWSUPERARROW"
	states {
		noAmmo: BUST B 0
			goto super::noAmmo
		select: BUST B 0 ACS_NamedExecuteWithResult("core_weaponcolor",0)
			goto super::select
		ready: BUST B 0
			goto super::ready
	
		altfire:
			BUST B 0 A_GiveInventory("altfireToken")
		fire:
			BUST B 0 A_JumpIfInventory("Super Arrow Cooldown", 1, "ready")
			BUST B 0 A_JumpIf(!CallACS("XW Ammo Check", 3), "noAmmo")
		
		shoot: 
			BUST B 0 A_JumpIfInventory("IsBot", 1, "botTrick")
			BUST B 0 ACS_NamedExecuteAlways("XW Ammo Use", 0, 3)
		main:
			BUST B 1 A_GiveInventory("Super Arrow Limit")
			BUST C 1
			BUST C 0 A_TakeInventory("SuperArrowSpeedStack")
			TNT1 A 0 ACS_NamedExecuteWithResult("core_nogravityplayer", 0, true)
			TNT1 A 0 ACS_NamedExecuteWithResult("XW", XW_LOCKER, 5)
			TNT1 A 0 A_Stop
			
			BUST C 0 A_JumpIf(z-floorz < 25, "frontalArrow")
			goto airArrow
		frontalArrow:
			BUST C 0 A_SpawnItemEx("Super Arrow Check", 60,0,28, 0,0,0,0, SXF_TRANSFERPITCH,0, TID+5770)
			XWAC J 0 A_JumpIfInventory("PowerSpreadRune", 1, "frontalSpread") goto preHold
		frontalSpread:
			BUST C 0 A_SpawnItemEx("Super Arrow Check", 48,-48,28, 0,0,0,0, SXF_TRANSFERPITCH,0, TID+5770)
			BUST C 0 A_SpawnItemEx("Super Arrow Check", 48,48,28, 0,0,0,0, SXF_TRANSFERPITCH,0, TID+5770)
			goto preHold
		airArrow:
			BUST C 0 A_SpawnItemEx("Super Arrow Check", 0,0,-25, 0,0,0,0, SXF_TRANSFERPITCH,0, TID+5770)
			XWAC J 0 A_JumpIfInventory("PowerSpreadRune", 1, "airSpread") goto preHold
		airSpread:
			BUST C 0 A_SpawnItemEx("Super Arrow Check", 0,-48,-25, 0,0,0,20, SXF_TRANSFERPITCH,0, TID+5770)
			BUST C 0 A_SpawnItemEx("Super Arrow Check", 0,48,-25, 0,0,0,-20, SXF_TRANSFERPITCH,0, TID+5770)
			goto preHold
			
		preHold:
			BUST C 1
			BUST D 0 A_PlaySoundEx("weapon/mbuster","Weapon")
			BUST D 4
			TNT1 A 0 ACS_NamedExecuteWithResult("core_nogravityplayer", 0, false)
			BUST D 0 A_JumpIfInventory("altfireToken", 1, "altRelease")
			BUST D 0 A_JumpIfInventory("isBot", 1, "altRelease")
			
		hold: althold:
			BUST D 1
			BUST D 0 A_Refire
			goto release
		altRelease:
			BUST D 1
		release:
			BUST D 0 A_TakeInventory("Super Arrow Limit", 1)
			BUST D 0 A_TakeInventory("altfireToken", 1)
			BUST D 0 A_GiveInventory("Super Arrow Cooldown")
			BUST B 0 A_ClearReFire
			BUST D 6
			BUST C 2 A_WeaponReady(WRF_NOFIRE)
			BUST D 0 A_JumpIfInventory("isBot", 1, "botWaitingRoom")
		end:
			BUST B 0
			goto ready
			
		botTrick: // they always use the air version
			BUST B 0 A_CheckFloor(1) goto noAmmo
			BUST B 3 ThrustThingZ(0, 40, 0, 0)
			goto main
		botWaitingRoom:
			BUST B 4 
			BUST B 0 A_JumpIfInventory("Super Arrow Speed", 1, "botWaitingRoom")
			goto end}}
			
actor "Super Arrow Cooldown" : Powerup { powerup.duration 25 }
actor "Super Arrow Limit" : Powerup { powerup.duration 70 }
actor "Super Arrow Hit" : PowerDamage { damagefactor "SuperArrow", 0.0 powerup.duration 3 }

actor "Super Arrow Frozen" : varBool {}
actor "Super Arrow Frozen Watcher Summon" : item { states {
	pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("core_stopplayer", TID-999, 1)
		TNT1 A 0 ACS_NamedExecuteWithResult("core_nogravityplayer", 0, true)
		TNT1 A 0 A_GiveInventory("Super Arrow Frozen", 1)
		TNT1 A 0 A_SpawnItemEx("Super Arrow Frozen Watcher")
		stop}}
		
actor "Super Arrow Frozen Watcher" : watcher {
	states {
		spawn: 
			TNT1 A 0
		idle:
			TNT1 A 2
			TNT1 A 0 A_JumpIfInTargetInventory("Super Arrow Speed", 1, "idle")
			TNT1 A 0 ACS_NamedExecuteWithResult("core_nogravityplayer", CallACS("core_gettarget")-999, false)
			TNT1 A 0 A_GiveToTarget("Super Arrow Unfreeze")
			stop}}
		
actor "Super Arrow Unfreeze" : item { states {
	pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("core_stopplayer", TID-999, 0)
		TNT1 A 0 A_TakeInventory("Super Arrow Frozen", 1)
		stop}}
		
		
		
		
		
			
// ------------ THE ARROW  --------------- //


actor "Super Arrow Check" : proj {
	// height 20+4 (player bobbing)
	damage (0) radius 15 height 20 +MOVEWITHSECTOR damage (0) 
	+DONTBLAST +DONTREFLECT species "XoverSuperArrow" +THRUSPECIES states {
		spawn: 
			TNT1 A 0 nodelay A_SetArg(0,TID)
			TNT1 A 0 Thing_ChangeTID(0,0)
			TNT1 A 0 A_ClearTarget
			TNT1 A 1
			TNT1 A 1 A_ChangeFlag("SOLID", true)
			TNT1 A 0 A_SpawnItemEx("Super Arrow", 0,0,0, 0,0,0, 0,SXF_TRANSFERTRANSLATION,0,args[0])
			stop
		destroyed: death:
		xdeath: crash: 
			TNT1 A 0 A_TakeFromTarget("Super Arrow Speed")
			TNT1 A 0 A_SpawnItemEx("ExplosionEffect1_4",0,0,8) stop}}
		

actor "Super Arrow" : "Super Arrow Check" {
	+SOLID +SHOOTABLE health 160 -NOBLOCKMAP mass 20000 +NOTARGETSWITCH
	+FORCEXYBILLBOARD 
	args 0,0 // [shooterTID, cursorFrame]
	states {
		spawn: 
			XWAJ K 0 nodelay A_SetArg(0, TID)
			XWAJ K 0 Thing_ChangeTID(0,0)
			TNT1 A 0 ACS_NamedExecuteWithResult("core_setpointer", AAPTR_TARGET,args[0]-5770)
			XWAJ K 0 A_ChangeFlag("MISSILE", false)
		stationary:
			//XWAJ K 0 A_JumpIf(!CallACS("core_targetexists"),"destroyed")
			XWAJ K 2 A_JumpIfInTargetInventory("Super Arrow Limit", 1, 1)
			goto release
			XWAJ K 1 A_GiveInventory("Super Arrow Aim 1", 1)
			XWAJ K 0 A_GiveInventory("Super Arrow Cursor Manager")
			goto stationary
			
		release:
			XWAJ K 0 A_ChangeFlag("MISSILE", true)
			XWAJ K 0 A_SpawnItemEx("Super Arrow Released",0,0,0,0,0,0,0,SXF_TRANSFERPITCH,0,6770+health)
			stop}}
			
// Carrying the player is ENTIRELY done through velocity change, 0 warping here
// that is because since projectiles can cross player-blocking linedefs, 
// warping might result in player going outside map limits
actor "Super Arrow Released" : "Super Arrow" {
	+MISSILE +STEPMISSILE maxstepheight 20 +NOEXPLODEFLOOR
	bouncecount 1 +BOUNCEONCEILINGS // fixes some online unsynch on user contact
	var int user_count; var int user_angle;
	damage (0) //(7+(args[4]*args[4])/240)
	args 0,0,0,0,0 // [ownerTID (6770), cursorAnimation, ownerTID (1000), health, velocity]
	states {
		spawn:
			XWAJ K 0 nodelay A_SetArg(3, health-(TID-6770))
			XWAJ K 0 Thing_ChangeTID(0,0)
			TNT1 A 0 A_JumpIf(!args[3], 2)
			TNT1 A 0 DamageThing(args[3])
			TNT1 A 0 A_SetUserVar("user_angle", (angle*256)/360)
			TNT1 A 0 A_SetArg(0, CallACS("core_gettarget")+5770)
			XWAJ K 0 A_SetArg(2, args[0]-5770)
			XWAJ K 0 A_SetUserVar("user_count", 5) // a little kick to get started
		woohoo1:
			XWAJ K 0 A_SetArg(4,((user_count*user_count)*0.045))
			XWAJ K 0 A_ChangeVelocity(
				args[4]*cos(pitch), 0, args[4]*sin(-pitch), CVF_REPLACE | CVF_RELATIVE)
			XWAJ K 0 A_GiveInventory("Super Arrow Damager Item")
			XWAJ K 1 ACS_NamedExecuteWithResult("XW", 14, args[2])			
		woohoo2:
			XWAJ K 0 A_JumpIfInTargetInventory("Super Arrow Hit", 1, "actorHit")
			XWAJ K 0 A_GiveInventory("Super Arrow Aim 2")
			XWAJ K 0 A_GiveInventory("Super Arrow Cursor Manager")			
			XWAJ K 0 A_GiveInventory("Super Arrow Damager Item")							
			XWAJ K 1 ACS_NamedExecuteWithResult("XW", 14, args[2])
			
		woohooEnd:
			XWAJ K 0 A_JumpIfInTargetInventory("Super Arrow Hit", 1, "actorHit")
			XWAJ K 0 A_JumpIfInTargetInventory("Super Arrow Limit", 1, "destroyed")
			XWAJ K 0 A_GiveInventory("Super Arrow Aim 2")
			XWAJ K 0 A_GiveInventory("Super Arrow Cursor Manager")
			// is player standing on the arrow ?
			XWAJ K 0 A_JumpIfInTargetInventory("Super Arrow Speed", 1, "speedStack")
			goto speedUp
		speedStack:
			XWAJ K 0 A_TakeFromTarget("SuperArrowSpeedStack")
			XWAJ K 0 A_GiveToTarget("SuperArrowSpeedStack", args[4])
			XWAJ K 0 A_JumpIf(args[4] < XW_SUPER_ARROW_INVUL_SPEED, "speedUp")
			XWAJ K 0 A_GiveToTarget("Super Arrow SS On")
		speedUp:
			XWAJ K 0 A_JumpIf(args[4] >= XW_SUPER_ARROW_MAX_SPEED, "woohoo1")
			XWAJ K 0 A_SetUserVar("user_count", user_count+1)
			goto woohoo1
						
		death: TNT1 A 0 A_JumpIfHealthLower(1, "destroyed")
			TNT1 A 0 A_CheckCeiling("destroyed") 
			goto wallArrow
		
		wallArrow: TNT1 A 0 A_SpawnItemEx("Super Arrow Wall Check") 
			TNT1 A 10 A_ChangeFlag("NOINTERACTION", true) stop
		
		xdeath: crash: actorHit: 
			TNT1 A 0 A_SpawnItemEx("__Super Arrow Drop")
			TNT1 A 0 A_TakeFromTarget("Super Arrow Speed")
			stop}}
			
actor "Super Arrow Damager Item" : item { states {
	pickup: TNT1 A 0 A_SpawnItemEx("Super Arrow Released Damager", 
		velx, vely, velz, velx, vely, velz,	7+(args[4]*args[4])/240, 
		SXF_ABSOLUTEVELOCITY | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEPOSITION) stop}}
			
actor "Super Arrow Released Damager" : proj { 
	radius 34 height 76 // radius 15 // arrow + player
	+DONTREFLECT +DONTBLAST
	damage (angle) damagetype "SuperArrow"	obituary "$OB_XWSUPERARROW" species "XoverSuperArrow" +THRUSPECIES
	states {
		spawn: TNT1 A 0
			TNT1 A 1
			stop
		death: TNT1 A 0
			stop
		xdeath: crash: TNT1 A 0 A_GiveToTarget("Super Arrow Hit", 1)
			stop}}
					
actor "Super Arrow Wall Check" : "Super Arrow" { +MISSILE damage (0)
	states {
		spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx("Super Arrow Wall")
			stop
		death: xdeath: crash: TNT1 A 0 A_TakeFromTarget("Super Arrow Speed")
			stop}}
			
actor "Super Arrow Wall" : "Super Arrow" { args 16 -SHOOTABLE
	states {
		spawn: 
			XWAJ L 0 nodelay A_PlaySoundEx("SUPRAROX", "Weapon")
			TNT1 A 0 A_JumpIfInTargetInventory("Super Arrow Speed", 1, 1)
			goto idle
			// fixes overmoving and falling. No warping, only 1-tic brutal ChangeVelocity
			TNT1 A 0 ACS_NamedExecuteWithResult("XW", 17)
			TNT1 A 0 A_TakeFromTarget("Super Arrow Speed")
		idle:
			XWAJ L 70
		fade:
			TNT1 A 1 A_SetArg(0,args[0]-1)
			XWAJ L 1 A_JumpIf(!args[0], "destroyed")
			loop}}
			
actor "__Super Arrow Drop" : watcher {
	states { spawn: TNT1 A 0 nodelay A_SpawnItemEx("Super Arrow Drop") stop}}
		
actor "Super Arrow Drop" : gfx { args 26 +FORCEXYBILLBOARD
	states {
		spawnframe: TNT1 A 0 A_PlaySoundEx("SUPRAROX", "Weapon")
			XWAJ L 10
		down:
			XWAJ L 2 ThrustThingZ(0, 8, 1, 1)
			XWAJ L 0 A_CheckFloor("end")
			XWAJ L 0 A_SetArg(0,args[0]-1)
			XWAJ L 0 A_JumpIf(!args[0], "end")
			loop}}
			
			
			
			
			

// ------------- ARROW AIM ----------- //

			
actor "Super Arrow Aim 1" : item {
	states {
		pickup: 
			XWAJ K 0 ACS_NamedExecuteWithResult("core_targetyawpitch",1)
			XWAJ K 0 A_JumpIf(pitch >= 270 && pitch <= XW_SUPER_ARROW_CAP_TOP, "fixUp")
			XWAJ K 0 A_JumpIf(pitch >= XW_SUPER_ARROW_CAP_BOT && pitch <= 90, "fixDown")
			goto end
		fixUp: XWAJ K 0 A_SetPitch(XW_SUPER_ARROW_CAP_TOP)		goto end
		fixDown: XWAJ K 0 A_SetPitch(XW_SUPER_ARROW_CAP_BOT) 	goto end
		end: 
			TNT1 A 0
			stop}}
				
			
actor "Super Arrow Aim 2" : "Super Arrow Aim 1" { states {
		pickup: 
			XWAJ K 0 A_JumpIf(
				CallACS("XW", 15, XW_SUPER_ARROW_H_ANGLE, z-floorz), "end")// pitch and angle limit
			goto super::pickup+1}}

/////////////////////////////////////////////////////////
// ----------- INVULNERABLE/DEFLECT STATUS ----------- //
/////////////////////////////////////////////////////////

actor "SuperArrowSpeedStack" : var8 { inventory.maxamount 20 } //XW_SUPER_ARROW_INVUL_SPEED
actor "Super Arrow SuperSonic" : PowerSpeed { 
		powerup.duration 0x7FFFFFFA		powerup.color "00 FF 00", 0.5 }
		
actor "Super Arrow Speed" : PowerDamage { 
	powerup.duration 15 damagefactor "SuperArrow", 3.0 }
actor "Super Arrow SS Stack" : var8 { inventory.maxamount 15 }
			
actor "Super Arrow SS On" : item {
	states {
		pickup: 
			TNT1 A 0 A_GiveInventory("Super Arrow SS Stack", 15)
			TNT1 A 0 A_JumpIfInventory("Super Arrow SuperSonic", 1, "end")
			TNT1 A 0 A_PlaySoundEx("SUPRAROG", "SoundSlot6")
			TNT1 A 0 ACS_NamedExecuteWithResult("core_invulnerableplayer",0,true)
			TNT1 A 0 A_ChangeFlag("REFLECTIVE", true)
			TNT1 A 0 A_ChangeFlag("SHIELDREFLECT", true)
			TNT1 A 0 A_GiveInventory("Super Arrow SuperSonic")
			TNT1 A 0 A_SpawnItemEx("Super Arrow SS Watcher")
		end: TNT1 A 0
			stop}}

actor "Super Arrow SS Watcher" : watcher {
	states {
		spawn:
			TNT1 A 0
		watch: 
			TNT1 A 0 A_JumpIf(!CallACS("core_targetexists"), "stopIt")
		comet1:
			TNT1 A 0 A_GiveToTarget("__Super Arrow Comet 1")
			TNT1 A 1 A_TakeFromTarget("Super Arrow SS Stack", 2)
			TNT1 A 1 A_GiveToTarget("__Super Arrow Comet 1")
			TNT1 A 0 A_JumpIfInTargetInventory("Super Arrow Speed", 1, "comet2")
			goto stopIt
		comet2:
			TNT1 A 0 A_GiveToTarget("__Super Arrow Comet 2")
			TNT1 A 1 A_TakeFromTarget("Super Arrow SS Stack", 2)
			TNT1 A 1 A_GiveToTarget("__Super Arrow Comet 2")
			TNT1 A 0 A_JumpIfInTargetInventory("Super Arrow Speed", 1, "comet3")
			goto stopIt
		comet3:
			TNT1 A 0 A_GiveToTarget("__Super Arrow Comet 3")
			TNT1 A 1 A_TakeFromTarget("Super Arrow SS Stack", 2)
			TNT1 A 1 A_GiveToTarget("__Super Arrow Comet 3")
			TNT1 A 0 A_JumpIfInTargetInventory("Super Arrow Speed", 1, "watch")
			goto stopIt
			
		stopIt:
			TNT1 A 0 A_GiveToTarget("Super Arrow SS Off")
			stop}}
			
						
actor "Super Arrow SS Off" : item {
	states {
		pickup: 
			TNT1 A 0 ACS_NamedExecuteWithResult("core_invulnerableplayer",0,false)
			TNT1 A 0 A_ChangeFlag("REFLECTIVE", false)
			TNT1 A 0 A_ChangeFlag("SHIELDREFLECT", false)
			TNT1 A 0 A_TakeInventory("Super Arrow SuperSonic")
			TNT1 A 0 A_PlaySoundEx("NOSOUND", "SoundSlot6")
			stop}}
			
			
			
			
			
			
			
////////////////////////////////////////////////////
// --------------- CURSOR / GFX ----------------- //	
///////////////////////////////////////////////////


// A_SetTranslation?
actor "Super Arrow Cursor Manager" : item {
	states {
		pickup: TNT1 A 0
			TNT1 A 0 A_SetArg(1, args[1]+1)
			TNT1 A 0 A_JumpIf((args[1]&0x7)>>2, "cursor2")
		cursor1:
			XWAJ K 0 A_SpawnItemEx("__Super Arrow Cursor 1", 0,0,0,0,0,0,0,
				SXF_TRANSFERPITCH , 0, args[0])
			stop
		cursor2:
			XWAJ K 0 A_SpawnItemEx("__Super Arrow Cursor 2", 0,0,0,0,0,0,0,
				SXF_TRANSFERPITCH , 0, args[0])
			stop}}

actor "__Super Arrow Cursor 1" : watcher {
	translation "192:192=176:176", "198:198=4:4"
	states {
		spawn:
			TNT1 A 0 nodelay A_SetScale(TID-6770, pitch+1)
		cursor:
			TNT1 A 0 A_SpawnItemEx("Super Arrow Cursor",0,0,0,0,0,0,0,
				SXF_TRANSFERPITCH | SXF_TRANSFERSCALE | SXF_TRANSFERTRANSLATION)
			stop}}
			
actor "__Super Arrow Cursor 2" : "__Super Arrow Cursor 1" {
	translation "192:192=4:4", "198:198=176:176"	
}
			
actor "Super Arrow Cursor" : "XW CS Frame" {
	states {
		spawn: TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteWithResult("core_setpointer", AAPTR_TARGET, scalex+1000)
			TNT1 A 0 A_SetPitch(scaley-1)
			TNT1 A 0 A_JumpIf(CallACS("XW CS", XW_CS_POV), "clientSidePOV")
			stop
		clientSidePOV:
			TNT1 A 0 A_SpawnItemEx("__Super Arrow Cursor GFX", 
				0,0,1, 
				1250*cos(pitch),0,1250*sin(-pitch), 
				0, SXF_TRANSFERTRANSLATION | SXF_TRANSFERPOINTERS)
			stop}}

			
	
actor "__Super Arrow Cursor GFX" : "XW Ray Cast" {
	species "XoverSuperArrow" +THRUSPECIES -THRUGHOST -RIPPER renderstyle normal
	radius 15 height 20 states {
		spawn:
			TNT1 A 0 
			TNT1 A 2
		death:
			TNT1 A 0 A_SpawnItemEx("Super Arrow Cursor GFX",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
			stop}}
		
			
actor "Super Arrow Cursor GFX" : gfx { +FORCEXYBILLBOARD
	states {
		spawn:
			XWAJ M 2
			stop}}
			
			
	
actor "__Super Arrow Comet 1" : item { states {
	pickup: TNT1 A 0 A_SpawnItemEx("Super Arrow Comet 1", 18, 0, 0, velx, vely, velz, 0, SXF_ABSOLUTEVELOCITY | SXF_TRANSFERTRANSLATION)
	stop}}
actor "__Super Arrow Comet 2" : item { states {
	pickup: TNT1 A 0 A_SpawnItemEx("Super Arrow Comet 2", 18, 0, 0, velx, vely, velz, 0, SXF_ABSOLUTEVELOCITY | SXF_TRANSFERTRANSLATION)
	stop}}
actor "__Super Arrow Comet 3" : item { states {
	pickup: TNT1 A 0 A_SpawnItemEx("Super Arrow Comet 3", 18, 0, 0, velx, vely, velz, 0, SXF_ABSOLUTEVELOCITY | SXF_TRANSFERTRANSLATION)
	stop}}
	
	
	
	
actor "Super Arrow Comet 1" : "XW CS Frame" { alpha 0.8 scale 6.0
	states {
		sprite:
			XWAJ N 0
			goto clientSideCheck
		otherPOV:
			"----" A 1
			"----" A 0 A_Stop
			"----" A 1
			"----" A 2 A_FadeOut(0.35)
			stop
		clientSidePOV:
			"----" A 2 A_FadeOut(0.6)
			stop}}
			
actor "Super Arrow Comet 2" : "Super Arrow Comet 1" { states {
		sprite: XWAJ O 0
			goto clientSideCheck}}
actor "Super Arrow Comet 3" : "Super Arrow Comet 1" { states {
		sprite: XWAJ P 0
			goto clientSideCheck}}